# zsh aliases

# chalias -- check if a command exists and if so make the alias {{{
# -d      -- check for $DISPLAY
# -s      -- make a suffix alias
chalias() {
    local suffix=''

    while [[ $1 == -* ]]; do
        case $1 in
            (-d) [[ -z $DISPLAY ]] && return;;
            (-s) suffix='-s' ;;
        esac
        shift
    done

    check_com ${argv[1,-2]} && alias $suffix ${argv[-1]}
} # }}}

# coreutils aliases {{{
if check_com gls; then
    alias ls='gls --color=auto'
elif [[ $(uname) == Linux ]]; then
    alias ls='ls --color=auto'
fi
alias dir=ls
alias egrep='grep -E'
# }}}

# apps-based aliases {{{
check_com mplayer || chalias mplayer2 mplayer=mplayer2

chalias hamachi ham='sudo -u hamachi hamachi -c /etc/hamachi'
chalias -d mplayer mplfs='mplayer -fs'
chalias mplayer stream='http_proxy= mplayer -volume 100 http://deskbert:9362/stream'
chalias mpc at mpat="echo 'mpc --no-status pause' | at"
chalias transmission-remote btr btg='btr -l | grep -i'
chalias wicd-curses wicd='wicd-curses'
# This allows aliases to be used with sudo. Credit to godlygeek.
chalias sudo sudo='sudo '
# }}}

# extension aliases {{{
chalias -d -s mupdf pdf=mupdf
chalias -s mono exe=mono

# pics
for i in gif jpg jpeg png
    chalias -d -s feh $i=feh

# videos
for i in avi flv mkv mov mp4 ogv wmv
    chalias -d -s mplayer $i=mplayer

unset i
# }}}
