# zsh hooks

function termtitle() { # {{{
    local wd
    if [[ $1 = '-p' ]]; then
        wd='%n@%m:%15<..<%~%<< '
        shift
    fi

    # forget the working directory if it's a long title
    (( ${#${*}} > 25 )) && unset wd

    # different terms have different escape sequences
    case $TERM in # {{{
        screen*)
            # output zsh for screen, then also output the xterm escape
            printf "\ek%s\e\\" ${*:-zsh}
            ;&
        *xterm*|*rxvt*)
            printf "\e]2;%s\a" ${(%)wd}${*}
            ;;
    esac # }}}
} # }}}

function clear_title() { # {{{
    [[ -t 1 ]] || return
    # show wd
    termtitle -p
} # }}}
add-zsh-hook chpwd clear_title
add-zsh-hook precmd clear_title

function set_title() { # {{{
    [[ -t 1 ]] || return
    local -a words

    # more or less taken from Frank Terbeck's preexec()
    words=( ${(z)${1}} )

    # obtain comment via reverse lookup
    # add one to the index of the first word equal to '#'
    local comment="${words[${words[(rei)#]}+1,-1]}"
    [[ -n $comment ]] && { termtitle $comment ; return }

    while (( ${#words} > 0 )); do # {{{
        case ${words[1]} in
            (*=*) ;& # variable assignments
            (sudo) ;&
            (exec) ;&
            (torify) ;&
            (watch) ;&
            (noglob) shift words ;;
            (*) break ;;
        esac
    done # }}}

    # loop through until the first command
    local w
    for w in $words; do
        if check_com $w; then
            termtitle -p $w
            break;
        fi
    done

} # }}}
add-zsh-hook preexec set_title

function add_to_history() { # {{{
    # The line without the trailing newline
    local line=${1%%$'\n'}

    # If the line ends in ' #', then remove the trailing # and whitespace, then
    # place it in the current history, change history list. Else just place the
    # line in the history list. We need to place the line in the history list to
    # get rid of the trailing ' #'.
    if [[ ${line[-2,-1]} == ' #' ]]; then
        line=${line%% ##\#}
        print -sr -- $line
        fc -p $histfile
    fi

    print -sr -- $line
    return 1
} # }}}
add-zsh-hook zshaddhistory set_title

function command_not_found_handler() { # {{{
    local -a funcs
    funcs=( ${^fpath}/$1(N) )

    if (( $#funcs > 0 )); then
        autoload -U $1
        # exec makes the return value from the function work without showing the
        # "command not found" message
        exec "$@"
    else
        return 1 # make zsh show "command not found"
    fi
} # }}}
