zstyle ':vcs_info:*' enable git hg
zstyle ':vcs_info:git:*' check-for-changes true

zstyle ':vcs_info:*' formats '[%b%f{%m%c%u}]'
zstyle ':vcs_info:*' actionformats '[%F{red}%a%f|%b{%m%c%u}]'
zstyle ':vcs_info:*' stagedstr '%F{green}S%f'
zstyle ':vcs_info:*' unstagedstr '%F{red}U%f'

# The next two hooks were originally taken from Seth House's blog post:
# <http://eseth.org/2010/git-in-zsh.html>
zstyle ':vcs_info:git+set-message:*' hooks git-remote git-stash

# Show remote ref name and number of commits ahead-of or behind
function +vi-git-remote() {
    local ahead behind remote
    local -a gitstatus

    # Are we on a remote-tracking branch?
    remote=${$(git rev-parse --verify ${hook_com[branch]}@{upstream} \
        --symbolic-full-name 2>/dev/null)/refs\/remotes\/}

    if [[ -n ${remote} ]]; then
        # for git prior to 1.7
        # ahead=$(git rev-list origin/${hook_com[branch]}..HEAD | wc -l)
        ahead=$(git rev-list ${hook_com[branch]}@{upstream}..HEAD 2>/dev/null | wc -l)
        (( $ahead )) && gitstatus+=( "%F{green}+${ahead}%f" )

        # for git prior to 1.7
        # behind=$(git rev-list HEAD..origin/${hook_com[branch]} | wc -l)
        behind=$(git rev-list HEAD..${hook_com[branch]}@{upstream} 2>/dev/null | wc -l)
        (( $behind )) && gitstatus+=( "%F{red}-${behind}%f" )

        hook_com[branch]="%F{green}${hook_com[branch]}%f:%F{cyan}${remote}%f"
        (( $#gitstatus > 0 )) && hook_com[branch]+="(${(j:/:)gitstatus})"
    fi
}

# Show count of stashed changes
function +vi-git-stash() {
    local -a stashes

    if [[ -s ${hook_com[base]}/.git/refs/stash ]]; then
        stashes=$(git stash list 2>/dev/null | wc -l)
        hook_com[misc]+="%F{yellow}${stashes}%f"
    fi
}
