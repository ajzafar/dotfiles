# zsh zle set up

# for bindings that should be available for both viins and vicmd
for keymap in viins vicmd; do
    alias bindkey="bindkey -M $keymap"

    bindkey ${key[Home]}    vi-beginning-of-line
    bindkey ${key[End]}     vi-end-of-line
    bindkey ${key[Insert]}  overwrite-mode
    bindkey ${key[Delete]}  delete-char
    bindkey ${key[Up]}      up-line-or-history
    bindkey ${key[Down]}    down-line-or-history
    bindkey ${key[Left]}    backward-char
    bindkey ${key[Right]}   forward-char

    bindkey '^R' history-incremental-pattern-search-backward
    bindkey '^Xr' history-incremental-pattern-search-forward
    bindkey '^A' vi-beginning-of-line
    bindkey '^E' vi-end-of-line
    bindkey '^U' kill-whole-line
    bindkey '^_' undo
    bindkey '^N' down-line-or-history
    bindkey '^P' up-line-or-history

    unalias bindkey
done

bindkey -M viins '^[^W' backward-delete-word
bindkey -M viins ${key[Backspace]} backward-delete-char

# Make ^L switch to vi-cmd mode
bindkey -M viins '^L' vi-cmd-mode
# But still let screen clearing be available
bindkey -M vicmd '^L' clear-screen
# Leave the minibuffer
bindkey -M isearch '^L' accept-search

# These names are kind of gross.
# Now with ^[u and ^-, we can easily change directories without repeatedly
# typing cd .. commands while also preserving our prompt.
function cd-zle_{..,-}() {
    cd ${WIDGET[(ws:_:)2]} > /dev/null
    zle reset-prompt
}
zle -N cd-zle_..
zle -N cd-zle_-

bindkey '^[u' cd-zle_..
bindkey '^[-' cd-zle_-

# rationalise-dot {{{
# This was written entirely by Mikael Magnusson
# Basically type '...' to get '../..' with successive .'s adding /..
function rationalise-dot {
    local MATCH # keep the regex match from leaking to the environment
    if [[ $LBUFFER =~ '(^|/| |      |'$'\n''|\||;|&)\.\.$' ]]; then
      LBUFFER+=/
      zle self-insert
      zle self-insert
    else
      zle self-insert
    fi
}
zle -N rationalise-dot
bindkey . rationalise-dot
# without this, typing a . aborts incremental history search
bindkey -M isearch . self-insert
# }}}
