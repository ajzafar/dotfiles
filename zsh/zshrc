# zshrc

# Prepend my fpath contributions
# Do this early on so my functions will be available in my setup
# and prepend so that my functions can override system functions
fpath=(
    $XDG_CONFIG_HOME/zsh/zfunctions
    $XDG_CONFIG_HOME/zsh/zfunctions/completions
    $fpath
)

check_com() { # {{{
    for arg in "$@"; do
        if [[ -n ${commands[$arg]}  ]] || \
           [[ -n ${aliases[$arg]}   ]] || \
           [[ -n ${functions[$arg]} ]]
        then
            return 0
        else
            return 1
        fi
    done
} # }}}

# source zsh files {{{
zfiles=(
    aliases
    compsys
    env
    hooks
    options
    vi-mode
    prompt
    zle
)

for f in $zfiles; do
    f=$XDG_CONFIG_HOME/zsh/$f
    if [[ -r $f ]]; then
        source $f
    else
        echo $f not found 1>&2
    fi
done
unset f zfiles
# }}}

# Keychain {{{
() {
    local -a keys
    keys=( ~/.ssh/id_[rd]sa(N) )
    if (( $#keys )) && check_com keychain; then
        keychain --agents ssh $keys
        source ~/.keychain/$HOST-sh
    fi
}
# }}}

# fortune {{{
if check_com fortune; then
    echo
    fortune
    echo
fi
# }}}
